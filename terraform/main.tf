# 1. RabbitMQ EC2
resource "aws_instance" "rabbitmq" {
  ami               = var.ami_id
  instance_type     = var.instance_type
  key_name          = var.key_name
  subnet_id         = var.subnet_id
  vpc_security_group_ids = [aws_security_group.rabbitmq_sg.id]
  user_data         = file("${path.module}/../scripts/install_rabbitmq.sh")

  tags = {
    Name    = "RabbitMQ-Server"
    Role    = "MessageBroker"
  }
}

# 2. Docker / API Rest EC2 (x2 - uno por AZ)
resource "aws_instance" "api_server" {
  count             = 2
  ami               = var.ami_id
  instance_type     = var.instance_type
  key_name          = var.key_name
  subnet_id         = count.index == 0 ? var.subnet_id : var.subnet_id_2
  vpc_security_group_ids = [aws_security_group.api_sg.id]

  user_data = templatefile("${path.module}/../scripts/install_api.sh", {
    mongodb_ip = aws_instance.mongodb.private_ip
    rabbitmq_ip = aws_instance.rabbitmq.private_ip
  })

  tags = {
    Name = "Docker-API-Server-${count.index + 1}"
    Role = "BackendAPI"
  }
}

# 3a. HAProxy Load Balancer EC2
# Corre HAProxy como contenedor Docker (igual que simple_balancer)
resource "aws_instance" "haproxy" {
  ami                    = var.ami_id
  instance_type          = var.instance_type
  key_name               = var.key_name
  subnet_id              = var.subnet_id
  vpc_security_group_ids = [aws_security_group.haproxy_sg.id]

  user_data = templatefile("${path.module}/../scripts/install_haproxy.sh", {
    api_server_1_ip = aws_instance.api_server[0].private_ip
    api_server_2_ip = aws_instance.api_server[1].private_ip
  })

  tags = {
    Name = "HAProxy-LoadBalancer"
    Role = "LoadBalancer"
  }
}

# 3. Worker EC2
resource "aws_instance" "worker" {
  ami               = var.ami_id
  instance_type     = var.instance_type
  key_name          = var.key_name
  subnet_id         = var.subnet_id
  vpc_security_group_ids = [aws_security_group.worker_sg.id]
  
  user_data = templatefile("${path.module}/../scripts/install_worker.sh", {
    mongodb_ip  = aws_instance.mongodb.private_ip
    rabbitmq_ip = aws_instance.rabbitmq.private_ip
  })

  tags = {
    Name    = "Worker-Server"
    Role    = "AsyncWorker"
  }
}

# 4. MongoDB EC2
resource "aws_instance" "mongodb" {
  ami                    = var.ami_id
  instance_type          = var.instance_type
  key_name               = var.key_name
  subnet_id              = var.subnet_id
  vpc_security_group_ids = [aws_security_group.mongodb_sg.id]
  user_data              = file("${path.module}/../scripts/install_mongodb.sh")

  tags = {
    Name = "MongoDB-Server"
    Role = "NoSQLDatabase"
  }
}

# ==========================================
# AWS Systems Manager Parameter Store
# ==========================================

resource "aws_ssm_parameter" "rabbitmq_ip" {
  name  = "/message-queue/dev/rabbitmq/public_ip"
  type  = "String"
  value = aws_instance.rabbitmq.public_ip
  description = "Public IP for RabbitMQ Server"
}

resource "aws_ssm_parameter" "api_ip" {
  name  = "/message-queue/dev/api/public_ip"
  type  = "String"
  value = aws_instance.haproxy.public_ip
  description = "IP pública del HAProxy Load Balancer EC2"
}

resource "aws_ssm_parameter" "worker_ip" {
  name  = "/message-queue/dev/worker/public_ip"
  type  = "String"
  value = aws_instance.worker.public_ip
  description = "Public IP for Async Worker Server"
}

resource "aws_ssm_parameter" "mongodb_ip" {
  name        = "/message-queue/dev/mongodb/public_ip"
  type        = "String"
  value       = aws_instance.mongodb.public_ip
  description = "Public IP for MongoDB Server"
}
